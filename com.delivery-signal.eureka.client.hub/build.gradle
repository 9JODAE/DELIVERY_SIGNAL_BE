plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.delivery-signal.eureka.client'
version = '0.0.1-SNAPSHOT'
description = 'hub-service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
def queryDslVersion = "7.1"
def querydslSrcDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

configurations {
    querydsl.extendsFrom compileClasspath
    compileOnly {
        extendsFrom annotationProcessor
    }
}

sourceSets {
    main {
        java {
            srcDirs += querydslSrcDir
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(querydslSrcDir))
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2025.0.0")
}

dependencies {
    // Spring WEB
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Eureka Client
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // PostgresSQL
    runtimeOnly 'org.postgresql:postgresql'

    // JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation("io.github.openfeign.querydsl:querydsl-core:${queryDslVersion}")
    implementation("io.github.openfeign.querydsl:querydsl-jpa:$queryDslVersion")
    annotationProcessor("io.github.openfeign.querydsl:querydsl-apt:$queryDslVersion:jpa")

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Redisson
    implementation 'org.redisson:redisson-spring-boot-starter:3.52.0'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    // OpenFeign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // Resilience4j Circuit Breaker
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.2.0'

    // Spring AOP
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Actuator & Micrometer & Zipkin
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'io.github.openfeign:feign-micrometer'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'

    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}