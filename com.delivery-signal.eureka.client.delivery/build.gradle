plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.delivery-signal.eureka.client'
version = '0.0.1-SNAPSHOT'
description = 'delivery-service'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

def queryDslVersion = "7.1"
def querydslSrcDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

// Q-Class 파일이 생성될 디렉토리 설정
sourceSets {
	main {
		java {
			srcDirs += querydslSrcDir
		}
	}
}

configurations {
	querydsl.extendsFrom compileClasspath
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2025.0.0")
}

dependencies {
	// JWT 관련
	compileOnly group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'

	// 서비스 간 통신 위한 Feign Client
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

	// RabbitMQ
	implementation 'org.springframework.boot:spring-boot-starter-amqp'

	// Delivery 서비스의 핵심 기능
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// Redis / Redisson
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.redisson:redisson-spring-boot-starter:3.20.0'

	// Eureka 클라이언트
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// QueryDSL
	implementation("io.github.openfeign.querydsl:querydsl-core:${queryDslVersion}")
	implementation("io.github.openfeign.querydsl:querydsl-jpa:$queryDslVersion")
	annotationProcessor("io.github.openfeign.querydsl:querydsl-apt:$queryDslVersion:jpa")

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    // Actuator & Micrometer & Zipkin
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'io.github.openfeign:feign-micrometer'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
